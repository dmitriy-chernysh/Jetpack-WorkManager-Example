// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.rootVersions = [
            "kotlin"      : "1.6.0",
            "gradle"      : "4.2.2",
            "playServices": "4.3.10",
            "koin"        : "3.1.4",
            "navigation"  : '2.3.5'
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$rootVersions.gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootVersions.kotlin"
        classpath "com.google.gms:google-services:$rootVersions.playServices"
        classpath "io.insert-koin:koin-gradle-plugin:$rootVersions.koin"
    }
}

// Gradle Doctor
// https://runningcode.github.io/gradle-doctor/
plugins {
    id("com.osacky.doctor") version "0.7.3"
}

// Gradle Doctor all settings here https://runningcode.github.io/gradle-doctor/configuration/
doctor {
    disallowMultipleDaemons = false
    negativeAvoidanceThreshold = 500
    warnWhenJetifierEnabled = true

    javaHome {
        ensureJavaHomeIsSet = true
        ensureJavaHomeMatches = true
        failOnError.set(true)
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    appVersionName = "1.0.0"
    appVersionCode = 1

    compileSdkVersion = 31
    minSdkVersion = 21
    targetSdkVersion = 31

    versions = [
            "appcompat"       : '1.3.1',
            "preference"      : "1.1.1",
            "material"        : '1.4.0',
            "fragment"        : '1.3.6',
            "activity"        : '1.3.1',
            "constraintLayout": '2.1.0',
            "coreKtx"         : '1.6.0',
            "commonUi"        : "1.1.1",
            "lifecycle"       : '2.3.1',
            "firebase"        : [
                    "bom": "29.0.0"
            ],
            "rx"              : [
                    "android": "2.1.1",
                    "kotlin" : "2.4.0"
            ],
            "room"            : "2.2.4",
            "leakcanary"      : '2.7',
            "playCore"        : '1.10.1',
            "playCoreKtx"     : '1.8.1',
            "workmanager"     : '2.7.1'
    ]

    deps = [
            "appcompat"         : "androidx.appcompat:appcompat:$versions.appcompat",
            "preference"        : "androidx.preference:preference:$versions.preference",
            "material"          : "com.google.android.material:material:$versions.material",
            "constraintLayout"  : "androidx.constraintlayout:constraintlayout:$versions.constraintLayout",
            "coreKtx"           : "androidx.core:core-ktx:$versions.coreKtx",
            "activity"          : "androidx.activity:activity-ktx:$versions.activity",
            "fragment"          : "androidx.fragment:fragment-ktx:$versions.fragment",

            //Navigation
            "navigationFragment": "androidx.navigation:navigation-fragment-ktx:$rootVersions.navigation",
            "navigationUi"      : "androidx.navigation:navigation-ui-ktx:$rootVersions.navigation",

            //Dynamic Feature support
            "navigationDynamic" : "androidx.navigation:navigation-dynamic-features-fragment:$rootVersions.navigation",
            "playCore"          : "com.google.android.play:core:$versions.playCore",
            "playCoreKtx"       : "com.google.android.play:core-ktx:$versions.playCoreKtx",

            //common
            "commonUI"          : "com.github.mobiledevpro:android-kotlin-mvvm-template:$versions.commonUi",

            //LifeCycle
            "viewModel"         : "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle",
            "liveData"          : "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle",
            "saveState"         : "androidx.lifecycle:lifecycle-viewmodel-savedstate:$versions.lifecycle",
            "lifecycleService"  : "androidx.lifecycle:lifecycle-service:$versions.lifecycle",
            "lifecycleCompiler" : "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle",

            //DI, Koin
            "koinCore"          : "io.insert-koin:koin-core:$rootVersions.koin",
            //Koin main features for Android (Scope,ViewModel ...)
            "koinAndroid"       : "io.insert-koin:koin-android:$rootVersions.koin",
            //Koin Java Compatibility
            "koinCompat"        : "io.insert-koin:koin-android-compat:$rootVersions.koin",
            //Koin Workmanager
            "koinWorkmanager"   : "io.insert-koin:koin-androidx-workmanager:$rootVersions.koin",

            //Rx
            "rxAndroid"         : "io.reactivex.rxjava2:rxandroid:$versions.rx.android",
            "rxKotlin"          : "io.reactivex.rxjava2:rxkotlin:$versions.rx.kotlin",

            //Room
            "room"              : "androidx.room:room-runtime:$versions.room",
            "roomKtx"           : "androidx.room:room-ktx:$versions.room",
            "roomCompiler"      : "androidx.room:room-compiler:$versions.room",
            "roomRx"            : "androidx.room:room-rxjava2:$versions.room",

            //testing
            "leakcanaryDebug"   : "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary",

            //Background
            "workmanager"       : "androidx.work:work-runtime:$versions.workmanager",
            "workmanagerKt"     : "androidx.work:work-runtime-ktx:$versions.workmanager",
            "workmanagerRx"     : "androidx.work:work-rxjava2:$versions.workmanager"

    ]


    core = [
            "navigation" : project(":core:navigation"),
            "utils"      : project(":core:utils"),
            "rx"         : project("core:rx"),
            "database"   : project("core:database"),
            "workmanager": project("core:workmanager")
    ]
}
